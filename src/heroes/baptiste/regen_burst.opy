#!mainFile "../../dev_main.opy"


rule "[baptiste/regen_burst.opy]: Remove initial burst healing from Regenerative Burst and set cooldown for duplicate":
    @Team 1
    @Event eachPlayer
    @Condition eventPlayer.hero_setup == Hero.BAPTISTE
    @Condition eventPlayer.isUsingAbility1()

    eventPlayer.setHealingDealt(0)
    waitUntil(eventPlayer.isUsingAbility1() == false, Math.INFINITY)
    eventPlayer.setHealingDealt(percent(eventPlayer._base_healing_scalar))
    if eventPlayer.getHeroOfDuplication() == Hero.BAPTISTE:
        eventPlayer.setAbilityCooldown(Button.ABILITY_1, ADJ_BAPTISTE_REGEN_BURST_COOLDOWN)


rule "[baptiste/regen_burst.opy]: Allow self healing during regenerative burst":
    @Team 1
    @Event eachPlayer
    @Condition eventPlayer.hero_setup == Hero.BAPTISTE
    @Condition eventPlayer.isUsingAbility1()

    pushSelfHealing(Button.ABILITY_1)
    wait(ADJ_BAPTISTE_REGEN_BURST_DURATION, Wait.RESTART_WHEN_TRUE)
    popSelfHealing(Button.ABILITY_1)


rule "[baptiste/regen_burst.opy]: Correct regenerative burst hps":
    @Team 1
    @Event playerDealtHealing
    @Condition eventPlayer.hero_setup == Hero.BAPTISTE
    @Condition eventAbility == Button.ABILITY_1
    
    if healee == healer: # self healing
        heal(
            healee, 
            healer, 
            ((eventHealing/eventPlayer._base_healing_scalar)*(ADJ_BAPTISTE_REGEN_BURST_SELF_HPS/OW2_BAPTISTE_REGEN_BURST_SELF_HPS) - eventHealing))
    else: # ally healing
        heal(
            healee, 
            healer, 
            ((eventHealing/eventPlayer._base_healing_scalar)*(ADJ_BAPTISTE_REGEN_BURST_ALLY_HPS/OW2_BAPTISTE_REGEN_BURST_ALLY_HPS) - eventHealing))
