#!mainFile "../../dev_main.opy"

#!include "heroes/zarya/self_bubble.opy"
#!include "heroes/zarya/ally_bubble.opy"


rule "[zarya/init.opy]: Initialize Zarya":
    @Event eachPlayer
    @Condition eventPlayer.call_init
    @Condition eventPlayer.hero_setup == Hero.ZARYA
    
    setCustomHp(
        ADJ_ZARYA_HEALTH,
        0,
        ADJ_ZARYA_SHIELDS_DUPLICATE if eventPlayer.isDuplicatingAHero() or SETTING_ECHO_TANK_HP else ADJ_ZARYA_SHIELDS
    )
    setUltCost(ADJ_ZARYA_ULT_COST)
    removeTankPassive()
    removeSelfHealing()

    # setBaseDamage(eventPlayer, ADJ_ZARYA_CANNON_DAMAGE_MAX/OW2_ZARYA_CANNON_DAMAGE_MAX)

    initSelfBubble()
    initAllyBubble()

    eventPlayer.call_init = false
    eventPlayer.max_health_scaler = (ADJ_ZARYA_BARRIER_HEALTH/OW2_ZARYA_BARRIER_HEALTH)


rule "[zarya/init.opy]: Clean up Zarya":
    @Event eachPlayer
    @Condition eventPlayer.hero_setup != Hero.ZARYA

    hideSelfBubbleCD()
    hideAllyBubbleCD()


rule "[zarya/init.opy]: Correct Zarya Primary Fire damage":
    @Event playerDealtDamage
    @Hero zarya
    @Condition eventAbility == Button.PRIMARY_FIRE
    
    damage(victim, attacker, eventDamage * (ADJ_ZARYA_CANNON_DAMAGE_MAX / OW2_ZARYA_CANNON_DAMAGE_MAX) - eventDamage)


rule "[zarya/init.opy]: Correct Zarya Alternate Fire damage":
    @Event playerDealtDamage
    @Hero zarya
    @Condition eventAbility == Button.SECONDARY_FIRE
    
    damage(victim, attacker, eventDamage * (ADJ_ZARYA_PARTICLE_DAMAGE_MAX / OW2_ZARYA_PARTICLE_DAMAGE_MAX) - eventDamage)
